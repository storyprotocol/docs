---
title: 라이선스 약관 등록
description: Solidity에서 새로운 라이선스 약관을 생성하는 방법을 배웁니다.
---

<Card title="Completed Code" href="https://github.com/storyprotocol/story-protocol-boilerplate/blob/main/test/1_LicenseTerms.t.sol" icon="thumbs-up">
  완성된 코드를 끝까지 따라가보세요.
</Card>

[License Terms](/concepts/licensing-module/license-terms)는 해당 약관이 적용된 IP에서 발행되는 라이선스에 대한 제한사항을 정의하는 구성 가능한 값들의 집합입니다. 예를 들어, "이 라이선스를 발행하면, 수익의 50%를 나와 공유해야 합니다." 전체 약관 세트는 다음에서 확인할 수 있습니다 [PIL Terms](/concepts/programmable-ip-license/pil-terms).

## 전제 조건

튜토리얼을 시작하기 전에 완료해야 할 몇 가지 단계가 있습니다.

1. 다음을 완료하세요 [Setup Your Own Project](/developers/smart-contracts-guide/setup)

## 시작하기 전에

알아야 할 중요한 점은 **생성하려는 동일한 매개변수 세트에 대한 라이선스 약관이 이미 존재하는 경우, 다시 생성할 필요가 없다는 것**입니다. 라이선스 약관은 프로토콜 전체에 적용되므로, 기존 라이선스 약관을 다음으로 사용할 수 있습니다 `licenseTermsId`.

## 라이선스 약관 등록

전체 약관 세트는 다음에서 확인할 수 있습니다 [PIL Terms](/concepts/programmable-ip-license/pil-terms).

테스트 파일을 다음 위치에 생성해 보겠습니다 `test/1_LicenseTerms.t.sol` 작동을 확인하고 결과를 검증하기 위해:

<Note>
  **컨트랙트 주소**

  Story 컨트랙트의 주소를 미리 입력해 두었습니다. 하지만 다음에서도 주소를 찾을 수 있습니다: [Deployed Smart Contracts](/developers/deployed-smart-contracts)
</Note>

```solidity test/1_LicenseTerms.t.sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import { Test } from "forge-std/Test.sol";
import { IPILicenseTemplate } from "@storyprotocol/core/interfaces/modules/licensing/IPILicenseTemplate.sol";
import { PILTerms } from "@storyprotocol/core/interfaces/modules/licensing/IPILicenseTemplate.sol";

// Run this test:
// forge test --fork-url https://aeneid.storyrpc.io/ --match-path test/1_LicenseTerms.t.sol
contract LicenseTermsTest is Test {
    address internal alice = address(0xa11ce);

    // For addresses, see https://docs.story.foundation/developers/deployed-smart-contracts
    // Protocol Core - PILicenseTemplate
    IPILicenseTemplate internal PIL_TEMPLATE = IPILicenseTemplate(0x2E896b0b2Fdb7457499B56AAaA4AE55BCB4Cd316);
    // Protocol Core - RoyaltyPolicyLAP
    address internal ROYALTY_POLICY_LAP = 0xBe54FB168b3c982b7AaE60dB6CF75Bd8447b390E;
    // Revenue Token - MERC20
    address internal MERC20 = 0xF2104833d386a2734a4eB3B8ad6FC6812F29E38E;

    function setUp() public {}

    /// @notice Registers new PIL Terms. Anyone can register PIL Terms.
    function test_registerPILTerms() public {
        PILTerms memory pilTerms = PILTerms({
            transferable: true,
            royaltyPolicy: ROYALTY_POLICY_LAP,
            defaultMintingFee: 0,
            expiration: 0,
            commercialUse: true,
            commercialAttribution: true,
            commercializerChecker: address(0),
            commercializerCheckerData: "",
            commercialRevShare: 0,
            commercialRevCeiling: 0,
            derivativesAllowed: true,
            derivativesAttribution: true,
            derivativesApproval: true,
            derivativesReciprocal: true,
            derivativeRevCeiling: 0,
            currency: MERC20,
            uri: ""
        });
        uint256 licenseTermsId = PIL_TEMPLATE.registerLicenseTerms(pilTerms);

        uint256 selectedLicenseTermsId = PIL_TEMPLATE.getLicenseTermsId(pilTerms);
        assertEq(licenseTermsId, selectedLicenseTermsId);
    }
}
```

### PIL Flavors

위에서 보듯이, 많은 약관 중에서 선택해야 합니다.

새로운 약관을 등록하는 데 도움이 되는 편의 함수들이 있습니다. [PIL Flavors](/concepts/programmable-ip-license/pil-flavors)를 만들었는데, 이는 자주 사용되는 라이선스 약관의 미리 구성된 조합으로 어떤 약관을 사용할지 결정하는 데 도움을 줍니다. 이러한 PIL Flavors를 확인하고 다음 편의 함수를 사용하여 약관을 등록할 수 있습니다:

<CardGroup cols={2}>
  <Card title="Non-Commercial Social Remixing" href="/concepts/programmable-ip-license/pil-flavors#flavor-%231%3A-non-commercial-social-remixing" icon="file">
    저작자 표시와 함께 무료 리믹스. 상업적 이용 불가.
  </Card>

  <Card title="Commercial Use" href="/concepts/programmable-ip-license/pil-flavors#flavor-%232%3A-commercial-use" icon="file">
    저작자 표시와 함께 라이선스 사용료 지불, 수익 공유는 필요 없음.
  </Card>

  <Card title="Commercial Remix" href="/concepts/programmable-ip-license/pil-flavors#flavor-%233%3A-commercial-remix" icon="file">
    저작자 표시와 함께 라이선스 사용료 지불 및 수익의 일정 비율 지불.
  </Card>

  <Card title="Creative Commons Attribution" href="/concepts/programmable-ip-license/pil-flavors#flavor-%234%3A-creative-commons-attribution" icon="file">
    귀속 표시와 함께 자유로운 리믹스와 상업적 사용이 가능합니다.
  </Card>
</CardGroup>

예시:

```solidity Solidity
import { PILFlavors } from "@storyprotocol/core/lib/PILFlavors.sol";

PILTerms memory pilTerms = PILFlavors.commercialRemix({
  mintingFee: 0,
  commercialRevShare: 5 * 10 ** 6, // 5% rev share
  royaltyPolicy: ROYALTY_POLICY_LAP,
  currencyToken: MERC20
});
```

## 코드를 테스트하세요!

실행 `forge build`. 모든 것이 성공적이라면, 명령어가 성공적으로 컴파일될 것입니다.

이제 다음 명령어를 실행하여 테스트를 실행하세요:

```bash
forge test --fork-url https://aeneid.storyrpc.io/ --match-path test/1_LicenseTerms.t.sol
```

## IP에 약관 첨부하기

축하합니다, 새로운 라이선스 약관을 만드셨습니다!

<Card title="Completed Code" href="https://github.com/storyprotocol/story-protocol-boilerplate/blob/main/test/1_LicenseTerms.t.sol" icon="thumbs-up">
  완성된 코드를 끝까지 따라가보세요.
</Card>

이제 새로운 라이선스 약관을 등록했으니, IP 자산에 이를 첨부할 수 있습니다. 이를 통해 다른 사람들이 라이선스를 발급받고 약관에 따라 제한된 방식으로 IP를 사용할 수 있게 됩니다.

다음 페이지에서 이에 대해 알아보겠습니다.
