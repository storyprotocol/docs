---
title: License Token 발행하기
description: Solidity에서 IPA로부터 License Token을 발행하는 방법을 알아보세요.
---

<Card title="Completed Code" href="https://github.com/storyprotocol/story-protocol-boilerplate/blob/main/test/3_LicenseToken.t.sol" icon="thumbs-up">
  완성된 코드를 끝까지 따라가보세요.
</Card>

이 섹션에서는 [License Token](/concepts/licensing-module/license-token)을 [IP Asset](/concepts/ip-asset/overview)으로부터 발행하는 방법을 보여줍니다. IP Asset에 [License Terms](/concepts/licensing-module/license-terms)가 첨부된 경우에만 License Token을 발행할 수 있습니다. License Token은 ERC-721로 발행됩니다.

License Token을 발행하는 데에는 두 가지 이유가 있습니다:

1. 라이선스를 보유하고 라이선스에 명시된 대로 기본 IP Asset을 사용할 수 있습니다(예: "적절한 출처를 표시하고 수익의 5%를 공유하는 한 상업적으로 사용할 수 있음")
2. 라이선스 토큰을 사용하여 다른 IP Asset을 파생물로 연결합니다. *하지만 나중에 보시겠지만, 일부 SDK 함수는 파생물을 등록하기 위해 먼저 라이선스 토큰을 명시적으로 발행할 필요가 없으며, 실제로 이를 백그라운드에서 처리합니다.*

## 전제 조건

튜토리얼을 시작하기 전에 완료해야 할 몇 가지 단계가 있습니다.

1. 완료하기 [Setup Your Own Project](/developers/smart-contracts-guide/setup)
2. IP Asset에 License Terms가 첨부되어 있어야 합니다. 이를 수행하는 방법은 [here](/developers/smart-contracts-guide/attach-terms)

## 라이선스 발행

IP Asset (`ipId = 0x01`)에 License Terms (`licenseTermdId = 10`)가 첨부되어 있다고 가정해 보겠습니다. 우리는 이 약관으로 2개의 License Token을 특정 지갑 주소(`0x02`)에 발행하고 싶습니다.

<Warning>
  **유료 라이선스**

  일부 IP Asset에는 라이선스를 발행하는 사용자가 `mintingFee`를 지불해야 하는 라이선스 약관이 첨부되어 있을 수 있음을 유의하세요.
</Warning>

작동하는 것을 보고 결과를 확인하기 위해 `test/3_LicenseToken.t.sol` 아래에 테스트 파일을 만들어 보겠습니다:

<Note>
  **컨트랙트 주소**

  Story 컨트랙트의 주소를 미리 입력해 두었습니다. 하지만 다음에서도 주소를 찾을 수 있습니다: [Deployed Smart Contracts](/developers/deployed-smart-contracts)
</Note>

```solidity test/3_LicenseToken.t.sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.26;

import { Test } from "forge-std/Test.sol";
// for testing purposes only
import { MockIPGraph } from "@storyprotocol/test/mocks/MockIPGraph.sol";
import { IIPAssetRegistry } from "@storyprotocol/core/interfaces/registries/IIPAssetRegistry.sol";
import { IPILicenseTemplate } from "@storyprotocol/core/interfaces/modules/licensing/IPILicenseTemplate.sol";
import { ILicensingModule } from "@storyprotocol/core/interfaces/modules/licensing/ILicensingModule.sol";
import { ILicenseToken } from "@storyprotocol/core/interfaces/ILicenseToken.sol";
import { RoyaltyPolicyLAP } from "@storyprotocol/core/modules/royalty/policies/LAP/RoyaltyPolicyLAP.sol";
import { PILFlavors } from "@storyprotocol/core/lib/PILFlavors.sol";
import { PILTerms } from "@storyprotocol/core/interfaces/modules/licensing/IPILicenseTemplate.sol";

import { SimpleNFT } from "../src/mocks/SimpleNFT.sol";

// Run this test:
// forge test --fork-url https://aeneid.storyrpc.io/ --match-path test/3_LicenseToken.t.sol
contract LicenseTokenTest is Test {
    address internal alice = address(0xa11ce);
    address internal bob = address(0xb0b);

    // For addresses, see https://docs.story.foundation/developers/deployed-smart-contracts
    // Protocol Core - IPAssetRegistry
    IIPAssetRegistry internal IP_ASSET_REGISTRY = IIPAssetRegistry(0x77319B4031e6eF1250907aa00018B8B1c67a244b);
    // Protocol Core - LicensingModule
    ILicensingModule internal LICENSING_MODULE = ILicensingModule(0x04fbd8a2e56dd85CFD5500A4A4DfA955B9f1dE6f);
    // Protocol Core - PILicenseTemplate
    IPILicenseTemplate internal PIL_TEMPLATE = IPILicenseTemplate(0x2E896b0b2Fdb7457499B56AAaA4AE55BCB4Cd316);
    // Protocol Core - RoyaltyPolicyLAP
    address internal ROYALTY_POLICY_LAP = 0xBe54FB168b3c982b7AaE60dB6CF75Bd8447b390E;
    // Protocol Core - LicenseToken
    ILicenseToken internal LICENSE_TOKEN = ILicenseToken(0xFe3838BFb30B34170F00030B52eA4893d8aAC6bC);
    // Revenue Token - MERC20
    address internal MERC20 = 0xF2104833d386a2734a4eB3B8ad6FC6812F29E38E;

    SimpleNFT public SIMPLE_NFT;
    uint256 public tokenId;
    address public ipId;
    uint256 public licenseTermsId;

    function setUp() public {
        // this is only for testing purposes
        // due to our IPGraph precompile not being
        // deployed on the fork
        vm.etch(address(0x0101), address(new MockIPGraph()).code);

        SIMPLE_NFT = new SimpleNFT("Simple IP NFT", "SIM");
        tokenId = SIMPLE_NFT.mint(alice);
        ipId = IP_ASSET_REGISTRY.register(block.chainid, address(SIMPLE_NFT), tokenId);

        licenseTermsId = PIL_TEMPLATE.registerLicenseTerms(
            PILFlavors.commercialRemix({
                mintingFee: 0,
                commercialRevShare: 10 * 10 ** 6, // 10%
                royaltyPolicy: ROYALTY_POLICY_LAP,
                currencyToken: MERC20
            })
        );

        vm.prank(alice);
        LICENSING_MODULE.attachLicenseTerms(ipId, address(PIL_TEMPLATE), licenseTermsId);
    }

    /// @notice Mints license tokens for an IP Asset.
    /// Anyone can mint a license token.
    function test_mintLicenseToken() public {
        uint256 startLicenseTokenId = LICENSING_MODULE.mintLicenseTokens({
            licensorIpId: ipId,
            licenseTemplate: address(PIL_TEMPLATE),
            licenseTermsId: licenseTermsId,
            amount: 2,
            receiver: bob,
            royaltyContext: "", // for PIL, royaltyContext is empty string
            maxMintingFee: 0,
            maxRevenueShare: 0
        });

        assertEq(LICENSE_TOKEN.ownerOf(startLicenseTokenId), bob);
        assertEq(LICENSE_TOKEN.ownerOf(startLicenseTokenId + 1), bob);
    }
}
```

## 코드를 테스트하세요!

실행 `forge build`. 모든 것이 성공적이라면, 명령이 성공적으로 컴파일될 것입니다.

이제 다음 명령을 실행하여 테스트를 실행하세요:

```bash
forge test --fork-url https://aeneid.storyrpc.io/ --match-path test/3_LicenseToken.t.sol
```

## 파생물 등록하기

<Card title="Completed Code" href="https://github.com/storyprotocol/story-protocol-boilerplate/blob/main/test/3_LicenseToken.t.sol" icon="thumbs-up">
  완성된 코드를 끝까지 따라가보세요.
</Card>

이제 License Token을 발행했으니, 이를 보유하거나 IP Asset을 파생물로 연결하는 데 사용할 수 있습니다. 다음 페이지에서 이에 대해 알아보겠습니다.
